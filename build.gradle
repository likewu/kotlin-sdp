buildscript {
    ext {
        kotlinVersion = '1.6.20'
        kotlinGradlePluginVersion = "1.6.20"
        junitPlatformGradlePluginVersion = "1.0.2"
        dokkaGradlePluginVersion = "1.7.10"
        detektGradlePluginVersion = "1.21.0"
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        gradlePluginPortal()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinGradlePluginVersion"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junitPlatformGradlePluginVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokkaGradlePluginVersion"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektGradlePluginVersion"
    }
}

plugins {
    id 'base'
    id 'idea'
    id 'org.jetbrains.kotlin.plugin.jpa' version "$kotlinVersion"
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'org.jetbrains.kotlin.plugin.spring' version "$kotlinVersion"
}

apply plugin: "kotlin"

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

apply plugin: "org.junit.platform.gradle.plugin"

junitPlatform {
    filters {
        engines {
            include "junit-jupiter"
        }
    }
}

apply plugin: "org.jetbrains.dokka"

//https://kotlin.github.io/dokka/1.7.10/user_guide/gradle/usage/
dokkaHtml {
    outputDirectory.set(file("${buildDir}/dokka"))
}

apply plugin: "io.gitlab.arturbosch.detekt"

//https://detekt.dev/docs/gettingstarted/gradle
detekt {
    config = files("$projectDir/detekt.yml")

    source = files(
            "src/main/kotlin",
            "gensrc/main/kotlin"
    )
}

apply plugin: "application"

mainClassName = "org.vld.sdp.MainKt"

ext {
    kotlinVersion = "1.6.20"
    junitJupiterVersion = "5.0.2"
    junitPlatformVersion = "1.0.2"
    assertJVersion = "3.8.0"
    mockitoVersion = "2.12.0"
    ktLintVersion = "0.14.0"
}

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    testCompile "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
    testCompile "org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion"
    testCompile "org.junit.platform:junit-platform-runner:$junitPlatformVersion"
    testCompile "org.assertj:assertj-core:$assertJVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
    testRuntime "org.junit.platform:junit-platform-console-standalone:$junitPlatformVersion"
    ktlint "com.github.shyiko:ktlint:$ktLintVersion"
    dokkaHtmlPlugin "org.jetbrains.dokka:kotlin-as-java-plugin:1.7.10"
}

// Usage: $ gradle junitPlatformTestConsole -PincludePackage=org.vld.sdp.behavioral
task junitPlatformTestConsole(type: JavaExec) {
    description = "JUnit Platform Test Console"
    jvmArgs "-ea"
    classpath = project.sourceSets.test.runtimeClasspath
    main = "org.junit.platform.console.ConsoleLauncher"
    args "--include-engine=junit-jupiter", "--scan-class-path", "--reports-dir=$project.testReportDir"
    if (project.hasProperty("includePackage")) {
        args += "--include-package=$includePackage"
    }
}

task ktLint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style"
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "--verbose", "src/**/*.kt"
}

check.dependsOn ktLint
task ktFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations"
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
